# Routing

# WAN interface

/interface ethernet {
  # ISP configuration is suggesting to try: fec-mode=fec91
  set [ find default-name=sfp28-2 ] auto-negotiation=yes fec-mode=off
}

# Bridging configuration

:if ([:len [ /interface bridge find where name=bridge1 ] ] = 0) do={
  /interface bridge {
    settings set use-ip-firewall=no

    add name=bridge1 protocol-mode=none

    port {
      remove [ find where bridge=bridge1 ]
      add bridge=bridge1 interface=sfp-sfpplus1
      add bridge=bridge1 interface=sfp-sfpplus2
      add bridge=bridge1 interface=sfp-sfpplus12
    }
  }
}

# Interface lists

/interface list {
  remove [ find where !dynamic && !builtin ]

  add name=wan
  add name=lan

  member {
    remove [ find ]
    add interface=sfp28-2 list=wan
    add interface=bridge1 list=lan
    add interface=lo list=lan
  }
}

# IPv4 configuration

/ip {
  address {
    remove [ find where interface=bridge1 && !dynamic ]
    add interface=bridge1 address=192.168.100.1/24
  }

  neighbor discovery-settings {
    set discover-interface-list=lan
  }
}

# IPv6 configuration

/ipv6 {
  address {
    remove [ find where interface=bridge1 && !dynamic ]
    add interface=bridge1 from-pool=pool-ipv6 address=::1 advertise=yes
  }

  nd {
    set [ find default=yes ] interface=bridge1 \
      managed-address-configuration=no other-configuration=no \
      advertise-mac-address=yes advertise-dns=yes \
      dns=2001:4860:4860::8888,2001:4860:4860::8844
  }
}

# Services relying on interface lists

/tool mac-server {
  mac-winbox set allowed-interface-list=lan
}


